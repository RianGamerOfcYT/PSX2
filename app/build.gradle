plugins {
    id 'com.android.application'
}

android {
    namespace 'com.izzy2lost.psx2'
    compileSdk 36
    ndkVersion '28.2.13676358'

    defaultConfig {
        applicationId "com.izzy2lost.psx2"
        minSdk 26
        targetSdk 36
        versionCode 1
        versionName "1.0"
        // APK
        setProperty("archivesBaseName","PSX2_${versionCode}_${new Date().format('yyyyMMddHHmm')}")

        externalNativeBuild {
            cmake {
                // Avoid LTO core on Android to prevent x86 objects from leaking into arm64 link
                arguments(
                        '-DANDROID=true',
                        '-DCMAKE_BUILD_TYPE=Release',
                        '-DANDROID_STL=c++_static'
                )
                // arguments '-DANDROID=true','-DCMAKE_BUILD_TYPE=Debug','-DANDROID_STL=c++_static'
            }
        }
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }
        // Placeholder to toggle profileable per buildType (for AGI)
        manifestPlaceholders = [
                profileable: "false"
        ]
    }

    buildTypes {
        release {
            ndk {
                // Keep symbol table for crash triage while testing
                debugSymbolLevel 'SYMBOL_TABLE'
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        // Build for profiling with Android GPU Inspector
        benchmark {
            // Release-like build, not debuggable, but profileable
            initWith release
            debuggable false
            // Use debug keystore to simplify installs
            signingConfig signingConfigs.debug
            // Turn on profileable in manifest via placeholder
            manifestPlaceholders = [ profileable: "true" ]
        }
    }
    // Relax lint for test release builds
    lint {
        checkReleaseBuilds false
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.14.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.documentfile:documentfile:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.activity:activity:1.10.1'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
}
