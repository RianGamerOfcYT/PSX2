plugins {
    id 'com.android.application'
}

// Function to get and increment version code
def getVersionCode() {
    def versionFile = file('version.properties')
    def versionCode = 3 // Starting version code
    
    if (versionFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(versionFile))
        versionCode = props['VERSION_CODE'] as int
    }
    
    // Increment version code for AAB builds
    if (gradle.startParameter.taskNames.any { it.toLowerCase().contains('bundle') }) {
        versionCode++
        def props = new Properties()
        props['VERSION_CODE'] = versionCode.toString()
        props.store(versionFile.newWriter(), null)
        println "Auto-incremented version code to: ${versionCode}"
    }
    
    return versionCode
}

android {
    namespace 'com.izzy2lost.psx2'
    compileSdk 36
    ndkVersion '28.2.13676358'

    defaultConfig {
        applicationId "com.izzy2lost.psx2"
        minSdk 26
        targetSdk 36
        versionCode getVersionCode()
        versionName "1.0.1"
        // APK
        setProperty("archivesBaseName","PSX2_${versionCode}_${new Date().format('yyyyMMddHHmm')}")

        externalNativeBuild {
            cmake {
                // Avoid LTO core on Android to prevent x86 objects from leaking into arm64 link
                arguments(
                        '-DANDROID=true',
                        '-DCMAKE_BUILD_TYPE=Release',
                        '-DANDROID_STL=c++_static',
                        '-DPCSX2_PROFILER=OFF'
                )
            }
        }
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
    }
    // Relax lint for test release builds
    lint {
        checkReleaseBuilds false
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        exclude 'META-INF/DEPENDENCIES'
    }
}

// Manual version increment tasks
task incrementVersion {
    doLast {
        def versionFile = file('version.properties')
        def versionCode = 3
        
        if (versionFile.exists()) {
            def props = new Properties()
            props.load(new FileInputStream(versionFile))
            versionCode = props['VERSION_CODE'] as int
        }
        
        versionCode++
        def props = new Properties()
        props['VERSION_CODE'] = versionCode.toString()
        props.store(versionFile.newWriter(), null)
        println "Incremented version code to: ${versionCode}"
    }
}

task showVersion {
    doLast {
        println "Current version code: ${getVersionCode()}"
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.14.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.documentfile:documentfile:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.activity:activity:1.10.1'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
}
